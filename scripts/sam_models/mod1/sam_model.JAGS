model{
  for(i in 1:N){
  # likelihood
    chl[i] ~ dnorm(mu[i], tau)

  # regression
    mu[i] <- B[1, inun[i]] +
             B[2, inun[i]] * Q[doy2003[i]] +
             B[3, inun[i]] * Qant[i, inun[i]]

  # Sum antecedent componentsacross all timesteps
    Qant[i,1] <- sum(qTemp1[i,])
    Qant[i,2] <- sum(qTemp2[i,])

  # Create temporary antecedent variable
    for(k in 1:nlagA){
      qTemp1[i,k] <-
        mean(Q[(doy2003[i]-k*pA):(doy2003[i]-k*pA+pA - 1)])*wA[k]
    }

    for(k in 1:nlagA){
      qTemp2[i,k] <-
        mean(Q[(doy2003[i]-k*pA):(doy2003[i]-k*pA+pA - 1)])*wB[k]
    }
  }

  #sum of the deltas for each covariate
  sumA <- sum(deltaA[])
  sumB <- sum(deltaB[])

  #priors for weights using the delta trick
  #daily variable weights
  for(k in 1:nlagA){
    wA[k] <- deltaA[k]/sumA
    deltaA[k] ~ dgamma(alphaA[k],1)

    wB[k] <- deltaB[k]/sumB
    deltaB[k] ~ dgamma(alphaB[k],1)
  }

  for(i in 1:3){
    for(j in 1:2){
      B[i,j] ~ dnorm(0, 0.001)
    }
  }

  #diffuse gamma priors for precision
  tau ~ dgamma(0.01, 0.01)
  sig <- 1/sqrt(tau)
}
