model{
  for(i in 1:N){
  # likelihood
    chl[i] ~ dnorm(mu[i], tau)

  # regression
    mu[i] <- B[1] +
             B[2] * past_topped[i] +
             B[3] * Q[doy1998[i]] +
             B[4] * Qant[i] +
             B[5] * Rant[i] +
             B[6] * Tant[i]

  # Sum antecedent componentsacross all timesteps
    Qant[i] <- sum(qTemp[i,])
    Rant[i] <- sum(rTemp[i,])
    Tant[i] <- sum(tTemp[i,])

  # Create temporary antecedent variable
    for(k in 1:nlagA){
      qTemp[i,k] <-
        mean(Q[(doy1998[i]-k*pA[k]):(doy1998[i]-k*pA[k]+pA[k] - 1)])*wA[k]
    }

    for(k in 1:nlagB){
      rTemp[i,k] <-
        mean(Rad[(doy1998[i]-k*pB[k]):(doy1998[i]-k*pB[k]+pB[k] - 1)])*wB[k]
    }

    for(k in 1:nlagC){
      tTemp[i,k] <-
        mean(Temp[(doy1998[i]-k*pC[k]):(doy1998[i]-k*pC[k]+pC[k] - 1)])*wC[k]
    }
  }

  #sum of the deltas for each covariate
  sumA <- sum(deltaA[])
  sumB <- sum(deltaB[])
  sumC <- sum(deltaC[])

  #priors for weights using the delta trick
  #daily variable weights
  for(k in 1:nlagA){
    wA[k] <- deltaA[k]/sumA
    deltaA[k] ~ dgamma(alphaA[k],1)
  }

  for(k in 1:nlagB){
    wB[k] <- deltaB[k]/sumB
    deltaB[k] ~ dgamma(alphaB[k],1)
  }

  for(k in 1:nlagC){
    wC[k] <- deltaC[k]/sumC
    deltaC[k] ~ dgamma(alphaC[k],1)
  }

  # Diffuse normal priors for regression coefficients
  for(i in 1:6){
    B[i] ~ dnorm(0, 0.001)
  }

  # Diffuse gamma priors for precision
  tau ~ dgamma(0.01, 0.01)
  sig <- 1/sqrt(tau)
}
